dnl Process this file with autoconf to produce a configure script.

AC_INIT(src/main.cc)
VERSION="0.14"
AM_INIT_AUTOMAKE(netdj,[$VERSION])
AM_CONFIG_HEADER(src/config.h)
AM_MAINTAINER_MODE

AC_DEFINE_UNQUOTED(PKGVER, "$PACKAGE v$VERSION",
                   [Package name and version])

dnl File setup
AC_DEFINE(CONF_FILENAME, "config",
          [Name of the configuration file])
AC_DEFINE(CONF_STATEFILENAME, "state",
          [Name of the state file])
AC_DEFINE(CONF_USERFILENAME, "users",
          [Name of the user file])
AC_DEFINE(CONF_LOGFILENAME, "playlog",
          [Name of log file])


dnl Directory setup
sysconfdir=$sysconfdir'/netdj'
NETDJ_ETCDIR=$sysconfdir
NETDJ_ETCDIR=`(
    test "x$prefix" = xNONE && prefix=$ac_default_prefix
    test "x$exec_prefix" = xNONE && exec_prefix=${prefix}
    eval echo "$NETDJ_ETCDIR"
)`
AC_DEFINE_UNQUOTED(NETDJ_ETCDIR,"$NETDJ_ETCDIR",
	           [Where to find global configuration files])
AC_SUBST(NETDJ_ETCDIR)

CONF_DIRNAME=".netdj/"
AC_DEFINE_UNQUOTED(CONF_DIRNAME,"$CONF_DIRNAME",
	           [Where to find local configuration files (~/CONF_DIRNAME)])
AC_SUBST(CONF_DIRNAME)

dnl Checks for programs.
AC_PROG_CXX
# if we're using GCC, enable all warnings
if test "$GXX" = yes; then
   CXXFLAGS="$CXXFLAGS -Wall";
fi
AC_PROG_INSTALL
AC_PROG_LN_S

dnl Check for POSIX threads
ACX_PTHREAD(,[AC_MSG_ERROR("Needs POSIX threads!")])
LIBS="$PTHREAD_LIBS $LIBS"
CXXFLAGS="$PTHREAD_CFLAGS $CXXFLAGS"

dnl Check for GNU Readline support
AC_ARG_WITH(readline,
        [  --without-readline      do not use GNU Readline],,
	USE_READLINE=yes)

if test "$USE_READLINE" = "yes"; then
  AC_DEFINE(USE_READLINE, 1,
            [Define if you want to enable readline support])
  AC_LIB_READLINE
  if test "$ac_cv_lib_readline" != "no"; then
    AC_CACHE_CHECK([for rl_icpfunc_t in history.h],
      [dj_icpfunc],[
      save_LIBS="$LIBS"
      LIBS="-lreadline $LIBS"
      AC_TRY_LINK([
    #include <stdio.h>
    #include <readline/history.h>
        ],
        [rl_icpfunc_t *func;],
        [dj_icpfunc=yes],[dj_icpfunc=no])
      LIBS="$save_LIBS"
    ])
    if test "$dj_icpfunc" = yes; then
      AC_DEFINE(HAVE_ICPFUNC, 1,
                [Define if you have icp_func defined])
    fi
  fi
fi

dnl Check for libraries
AC_CHECK_LIB(shout, shout_update_metadata)

dnl Checks for header files.
AC_CHECK_HEADERS(errno.h,,[AC_MSG_ERROR("Needs errno.h!")])
AC_CHECK_HEADERS(regex.h,,[AC_MSG_ERROR("Needs regex.h!")])
AC_HEADER_STDC

AC_OUTPUT([
Makefile
config/Makefile
doc/Makefile
doc/netdj.1
src/Makefile
util/Makefile
netdj.spec
])
